import java.time.Duration

plugins {
	// Apply the java-library plugin to add support for Java Library
	id 'java-library'
	id 'jacoco'
	id "org.owasp.dependencycheck" version "7.1.1"
	id 'org.sonarqube' version '4.0.0.2929'

	id 'maven-publish'
	id 'signing'
	id 'org.ajoberstar.grgit' version '4.1.1'

	id 'io.codearte.nexus-staging' version '0.30.0'
	id 'de.marcphilipp.nexus-publish' version '0.4.0'

	id 'com.github.hierynomus.license' version '0.16.1'
	id 'com.github.hierynomus.license-report' version '0.16.1'

	id 'se.patrikerdes.use-latest-versions' version '0.2.18'
	id 'com.github.ben-manes.versions' version '0.42.0'

	id 'com.diffplug.spotless' version '6.9.0'

	id 'idea'
}

project.version = CURRENT_VERSION
group 'com.acrolinx.client'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

def devId = "Acrolinx Open Source"
def devName = "Acrolinx"
def devEmail = "opensource@acrolinx.com"

def buildNumber = System.getenv('TRAVIS_BUILD_NUMBER') || System.getenv('BUILD_NUMBER');

// def buildNumber = System.getenv('BUILD_NUMBER')

def buildVersion = buildNumber != null ? buildNumber : 123

def artifactName = 'sdk'

def fullVersion = "${project.version}.${buildVersion}"

def isReleaseBuild = !fullVersion.contains('SNAPSHOT')

def sonatypeUsername = hasProperty('sonatypeUsername') && sonatypeUsername.length() > 0 ? sonatypeUsername : System.getenv('SONATYPE_USERNAME')
def sonatypePassword = hasProperty('sonatypePassword') && sonatypePassword.length() > 0 ? sonatypePassword : System.getenv('SONATYPE_PW')

repositories {
	mavenCentral()
	maven {
		name = "ajoberstar-backup"
		url = "https://ajoberstar.org/bintray-backup/"
	}
}

jacocoTestReport {
	reports {
		xml.required = true
		html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
	}
}

dependencyCheck {
	formats = ['HTML', 'XML']
	cve {
		urlModified = 'https://dune.dev.acrolinx.com/~nist-data-mirror/nvdcve-1.1-modified.json.gz'
		urlBase = 'https://dune.dev.acrolinx.com/~nist-data-mirror/nvdcve-1.1-%d.json.gz'
	}
}

sonarqube {
	properties {
		property "sonar.organization", "acrolinx"
		property 'sonar.projectKey', 'acrolinx_sdk-java'
		property "sonar.exclusions", "src/test/**/*.java"
		property "sonar.host.url", "https://sonarcloud.io"
	}
}

dependencies {
	implementation 'org.slf4j:slf4j-api:2.0.7'
	implementation 'com.google.guava:guava:31.1-jre'
	implementation 'com.google.code.gson:gson:2.10.1'
	implementation 'io.gsonfire:gson-fire:1.8.5'
	implementation 'org.apache.httpcomponents:httpclient:4.5.14'

	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
	testImplementation 'io.github.cdimascio:java-dotenv:5.2.2'
	testImplementation "com.github.tomakehurst:wiremock-jre8-standalone:2.35.0"
	testImplementation "org.mockito:mockito-core:4.11.0"
	testImplementation 'org.hamcrest:hamcrest:2.2'
	testRuntimeOnly 'ch.qos.logback:logback-classic:1.3.7'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
}


task sourcesJar(type: Jar) {
	archiveClassifier = 'sources'
	from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
	archiveClassifier = 'javadoc'
	from javadoc.destinationDir
}

license {
	header rootProject.file('LICENSE_HEADER')
	strictCheck true
	skipExistingHeaders true
	excludes(["**/*.json", "**/*.xml", "src/test/resources/*"])
}

downloadLicenses {
	includeProjectDependencies true
	dependencyConfiguration = "compileClasspath"
}

test {
	useJUnitPlatform()
	testLogging {
		events "failed"
		exceptionFormat "short"
	}
}

jar {
	archiveFileName = "${artifactName}-${fullVersion}.jar"
	manifest {
		attributes(
				"Implementation-Title": "${artifactName}",
				"Implementation-Version": "${fullVersion}",
				"Specification-Title": "${artifactName}",
				"Specification-Version": project.version
		)

	}
	from("$buildDir/reports/license") {
		include 'license-dependency.html'
	}
	from(project.projectDir) {
		include 'LICENSE'
		into 'META-INF'
	}
}

artifacts {
	archives jar
	archives javadocJar
	archives sourcesJar
}

publishing {
	repositories {
		maven {
			if (sonatypeUsername == null) {
				url "$buildDir/repo"
			} else if (isReleaseBuild) {
				url "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
				credentials {
					username = sonatypeUsername
					password = sonatypePassword
				}
			} else if (!isReleaseBuild) {
				url "https://oss.sonatype.org/content/repositories/snapshots/"
				credentials {
					username = sonatypeUsername
					password = sonatypePassword
				}
			}
		}
	}
	publications {
		mavenJava(MavenPublication) {
			groupId = 'com.acrolinx.client'
			artifactId = 'sdk'
			from components.java
			artifact sourcesJar
			artifact javadocJar

			pom {
				name = 'Acrolinx Platform SDK'
				description = 'A library to interact with the Acrolinx Platform.'
				url = 'https://github.com/acrolinx/sdk-java'
				scm {
					url = 'https://github.com/acrolinx/sdk-java'
					connection = 'scm:git@github.com:acrolinx/sdk-java.git'
					developerConnection = 'scm:git@github.com:acrolinx/sdk-java.git'
				}
				licenses {
					license {
						name = 'The Apache Software License, Version 2.0'
						url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						distribution = 'repo'
					}
				}
				developers {
					developer {
						id = devId
						name = devName
						email = devEmail
						organization = 'Acrolinx'
						organizationUrl = 'https://www.acrolinx.com/'
					}
				}
				versionMapping {
					usage('java-api') {
						fromResolutionOf('runtimeClasspath')
					}
					usage('java-runtime') {
						fromResolutionResult()
					}
				}
			}
		}
	}
}

nexusPublishing {
	connectTimeout = Duration.ofMinutes(5);
	clientTimeout =  Duration.ofMinutes(5);
	repositories {
		sonatype {
			username = sonatypeUsername
			password = sonatypePassword
		}
	}
}

signing {
	required { isReleaseBuild }
	def signingKey = findProperty("signingKey")
	def signingPassword = findProperty("signingPassword")
	useInMemoryPgpKeys(signingKey, signingPassword)
	sign publishing.publications.mavenJava
}

task createGithubReleaseTag {
	doLast {
		println 'Creating Github Release Tag'
		if (isReleaseBuild) {
			def hasOldReleaseVersion = grgit.tag.list().find { it.getName() == "release-${project.version}" }
			if (!hasOldReleaseVersion) {
				println 'is new release version'
			}
		} else {
			println 'NO RELEASE VERSION'
			throw new GradleException('NO APPROPRIATE RELEASE VERSION')
		}
	}
}


task checkLicenseHeaders {
	doLast {
		println 'If this fails run \'./gradlew licenseFormat\' to apply missing headers'
	}
}

tasks.withType(Javadoc) {
	options.addStringOption('Xdoclint:none', '-quiet')
	options.memberLevel = JavadocMemberLevel.PUBLIC
}

nexusStaging{
	numberOfRetries = 40
	delayBetweenRetriesInMillis = 4000
	username = sonatypeUsername
	password = sonatypePassword
}

spotless {
	java {
		target rootProject.fileTree('.') {
			include '**/*.java'
			exclude '**/*.license.java'
		}
		importOrderFile		rootProject.file('spotless.importorder')			// An import ordering file, exported from Eclipse
		eclipse().configFile rootProject.file('spotless.eclipseformat.xml')		// XML file dumped out by the Eclipse formatter
		custom 'Lambda fix', { it.replace('} )', '})').replace('} ,', '},') }
	}
	format 'misc', {
		target rootProject.fileTree('.') {
			include '**/*.gradle'
			include '**/*.gitignore'
			'***//*.sh'
			exclude  'src/test/**'
		}

		indentWithTabs()
		trimTrailingWhitespace()
		endWithNewline()
	}
}

licenseMain.dependsOn checkLicenseHeaders
licenseTest.dependsOn checkLicenseHeaders
jar.dependsOn 'downloadLicenses'
test.dependsOn javadoc
check.dependsOn 'spotlessCheck'
javadocJar.dependsOn javadoc
spotlessJava.dependsOn 'downloadLicenses'
spotlessJava.dependsOn test
spotlessJava.dependsOn 'compileJava'
spotlessJava.dependsOn 'javadoc'
spotlessMisc.dependsOn spotlessJava
