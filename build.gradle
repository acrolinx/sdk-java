import java.time.Duration

plugins {
    id 'java-library'
    id 'jacoco'
    id 'maven-publish'
    id 'signing'
    id 'org.ajoberstar.grgit' version '4.1.1'
    id 'io.codearte.nexus-staging' version '0.30.0'
    id 'de.marcphilipp.nexus-publish' version '0.4.0'
    id 'com.github.hierynomus.license-report' version '0.16.1'
    id 'org.sonarqube' version '4.0.0.2929'
    id 'com.diffplug.spotless' version '6.13.0'
}

dependencies {
    implementation 'org.slf4j:slf4j-api:2.0.9'
    implementation 'com.google.guava:guava:32.1.3-jre'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'io.gsonfire:gson-fire:1.9.0'
    implementation 'org.apache.httpcomponents:httpclient:4.5.14'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'
    testImplementation 'io.github.cdimascio:dotenv-java:2.3.0'
    testImplementation 'com.github.tomakehurst:wiremock-jre8:2.35.1'
    testImplementation 'org.mockito:mockito-core:4.11.0'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testRuntimeOnly 'ch.qos.logback:logback-classic:1.3.8'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

repositories {
    mavenCentral()
    maven {
        name = "ajoberstar-backup"
        url = "https://ajoberstar.org/bintray-backup/"
    }
}

sonar {
    properties {
        property "sonar.organization", "acrolinx"
        property "sonar.projectKey", "acrolinx_sdk-java"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.memberLevel = JavadocMemberLevel.PUBLIC
}

project.version = currentVersion
group = 'com.acrolinx.client'

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

def buildNumber = System.getenv('BUILD_NUMBER');
def buildVersion = buildNumber != null ? buildNumber : 123
def artifactName = "sdk"
def fullVersion = "${project.version}.${buildVersion}"
def isReleaseBuild = !fullVersion.contains('SNAPSHOT')

def sonatypeUsername = hasProperty('sonatypeUsername') && sonatypeUsername.length() > 0 ? sonatypeUsername : System.getenv('SONATYPE_USERNAME')
def sonatypePassword = hasProperty('sonatypePassword') && sonatypePassword.length() > 0 ? sonatypePassword : System.getenv('SONATYPE_PW')

jar {
    archiveFileName = "${artifactName}-${fullVersion}.jar"
    manifest {
        attributes(
                "Implementation-Title": "${artifactName}",
                "Implementation-Version": "${fullVersion}",
                "Specification-Title": "${artifactName}",
                "Specification-Version": project.version
        )

    }
    from("$buildDir/reports/license") {
        include 'license-dependency.html'
    }
    from(project.projectDir) {
        include 'LICENSE'
        into 'META-INF'
    }
}

def devId = "Acrolinx Open Source"
def devName = "Acrolinx"
def devEmail = "opensource@acrolinx.com"

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'com.acrolinx.client'
            artifactId = 'sdk'
            from components.java

            pom {
                name = 'Acrolinx Platform SDK'
                description = 'A library to interact with the Acrolinx Platform.'
                url = 'https://github.com/acrolinx/sdk-java'
                scm {
                    url = 'https://github.com/acrolinx/sdk-java'
                    connection = 'scm:git@github.com:acrolinx/sdk-java.git'
                    developerConnection = 'scm:git@github.com:acrolinx/sdk-java.git'
                }
                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = devId
                        name = devName
                        email = devEmail
                        organization = 'Acrolinx'
                        organizationUrl = 'https://www.acrolinx.com/'
                    }
                }
                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }
            }
        }
    }
    repositories {
        maven {
            if (isReleaseBuild) {
                url "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = sonatypeUsername
                    password = sonatypePassword
                }
            } else if (sonatypeUsername == null) {
                url "$buildDir/repo"
            } else if (!isReleaseBuild) {
                url "https://oss.sonatype.org/content/repositories/snapshots/"
                credentials {
                    username = sonatypeUsername
                    password = sonatypePassword
                }
            }
        }
    }
}

nexusStaging {
    numberOfRetries = 40
    delayBetweenRetriesInMillis = 4000
    username = sonatypeUsername
    password = sonatypePassword
}

nexusPublishing {
    connectTimeout = Duration.ofMinutes(5);
    clientTimeout = Duration.ofMinutes(5);
    repositories {
        sonatype {
            username = sonatypeUsername
            password = sonatypePassword
        }
    }
}

signing {
    required { isReleaseBuild }
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}

task createGithubReleaseTag {
    doLast {
        println 'Creating Github Release Tag'
        if (isReleaseBuild) {
            grgit.open(currentDir: rootDir)
            def hasOldReleaseVersion = grgit.tag.list().find { it.getName() == "release-${project.version}" }
            println 'Finding tags'
            if (!hasOldReleaseVersion) {
                println 'is new release version'
            } else {
                println 'This version was already released'
            }
        } else {
            println 'NO RELEASE VERSION'
            throw new GradleException('NO APPROPRIATE RELEASE VERSION')
        }
    }
}

downloadLicenses {
    includeProjectDependencies true
    dependencyConfiguration = "compileClasspath"
}

test {
    useJUnitPlatform()
    testLogging {
        events "failed"
        exceptionFormat "short"
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
    }
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        googleJavaFormat()
        licenseHeader '/* Copyright (c) $YEAR-present Acrolinx GmbH */'
    }
    format 'misc', {
        target '*.gradle', '*.md', '.gitignore', '*.sh'

        indentWithSpaces()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

jar.dependsOn 'downloadLicenses'
test.dependsOn javadoc
check.dependsOn 'spotlessCheck'
